# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>

pkgname=py3status
pkgver=1.4
pkgrel=1
pkgdesc="An extensible i3status wrapper written in python"
url="http://www.ultrabug.fr/tag/py3status/"
arch=('any')
license=('custom: Simplified BSD')
depends=('python')
conflicts=("$pkgname-git") # this can be removed, if the -git package gets updated!
optdepends=('i3status: for most of the functionality')
source=($pkgname-$pkgver.tar.gz::"https://github.com/ultrabug/py3status/archive/$pkgver.tar.gz")
sha256sums=('cf1c202c44a3ea7515f2fad0fc61509dada8ec3dbec3228951a9a5fdbee8c106')

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir/" --optimize=1
  # doc
  install -d "$pkgdir"/usr/share/doc/$pkgname/examples
  install -m644 README.md CHANGELOG "$pkgdir"/usr/share/doc/$pkgname
  install -m644 examples/* "$pkgdir"/usr/share/doc/$pkgname/examples
  # license
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
