# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=qbittorrent
pkgver=3.1.5
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt4 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org"
license=('custom GPL2')
depends=('qt4' 'libtorrent-rasterbar')
makedepends=('boost')
optdepends=('python: needed for torrent search tab')
install=$pkgname.install
source=("http://downloads.sourceforge.net/sourceforge/qbittorrent/$pkgname-$pkgver.tar.xz")
sha256sums=('db48b79565f88a1ffe30b6adababc5b4d694324e953e06123751a1f52e59dcc7')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make INSTALL_ROOT="$pkgdir/" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
