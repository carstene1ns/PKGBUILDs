# Maintainer: carstene1ns <arch carsten-teibes de>
# Contributor: Marco S <marcosiviero.mobile@gmail.com>
# Contributer: Paul Barker <paul@paulbarker.me.uk>

_pkgbase=poole
pkgname=$_pkgbase-py3-hg
pkgver=189.43f18bfd8e4a
pkgrel=1
pkgdesc="A damn simple static website generator (python3 branch)"
arch=('i686' 'x86_64')
url="https://bitbucket.org/obensonne/poole"
license=('GPL3')
conflicts=('poole-hg')
depends=('python-markdown')
makedepends=('mercurial')
source=('hg+https://bitbucket.org/obensonne/poole#branch=py3')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"

  printf "%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  # patch theme directory
  sed -i "s|opj(HERE, 'themes')|'/usr/share/$_pkgbase/themes'|" \
    "$srcdir/$_pkgbase/$_pkgbase.py"
}

package() {
  install -Dm755 "$srcdir/$_pkgbase/$_pkgbase.py" "$pkgdir/usr/bin/$_pkgbase"
  install -d "$pkgdir/usr/share/$_pkgbase"
  cp -r "$srcdir/$_pkgbase/themes/" "$pkgdir/usr/share/$_pkgbase"
}
