# Maintainer: carstene1ns <arch carsten-teibes de>
# Contributors: atommixz, beroal

_pkgbase=libtorrent-rasterbar
pkgname=$_pkgbase-svn
pkgver=r9531
pkgrel=1
pkgdesc="C++ library that aims to be a good bittorrent implementation (development version)."
arch=('i686' 'x86_64')
url="http://www.rasterbar.com/products/libtorrent/"
license=('BSD')
depends=('boost-libs' 'python2' 'geoip')
makedepends=('boost' 'subversion')
conflicts=('libtorrent-rasterbar')
provides=('libtorrent-rasterbar')
source=($_pkgbase::"svn+https://svn.code.sf.net/p/libtorrent/code/trunk")
md5sums=('SKIP')

pkgver() {
  cd $_pkgbase

  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd $_pkgbase

  export PYTHON="/usr/bin/python2"

  ./autotool.sh
  ./configure --prefix=/usr --enable-python-binding --with-libgeoip=system
  make
}

package() {
  cd $_pkgbase

  make DESTDIR="$pkgdir/" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
