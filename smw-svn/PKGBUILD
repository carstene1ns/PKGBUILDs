# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Evangelos Foutras <foutrelis gmail com>
# Contributer: Matthew Bauer <mjbauer95 gmail com>

pkgname=smw-svn
pkgver=1.8.r6
pkgrel=1
pkgdesc="Super Mario War multiplayer game (development version)"
arch=('i686' 'x86_64')
url="http://smw.supersanctuary.net/"
license=('GPL2')
depends=('sdl_mixer' 'sdl_image')
makedepends=('dos2unix' 'subversion')
provides=('smw')
conflicts=('smw')
source=("smw::svn+http://supermariowar.googlecode.com/svn/trunk/"
        "smw.desktop"
        "smw.png"
        "gcc.patch"
        "libpng.patch")
md5sums=('SKIP'
         'SKIP'
         'b7f5ef181e41eb0339be746ea03ff628'
         '586cf917af0a81912d8c5c0fcfddb64b'
         'ee4c6fd9e7f6f9a4dd699cdcd028b72f')
sha256sums=('SKIP'
            'SKIP'
            '28299d22e9ed8ebdecc8d936bfebef4a13381d20afda881c87661debe33f17d6'
            '7509d33b70b11742fc83cb62dd3e70a51d35596f20eb3a38b3553946261fe986'
            'be105515dbc850fcc8f73d894555ba71a7b89c52d7af2aa329a40f8088c2c75d')
pkgver() {
  cd smw

  local ver="$(svnversion)"
  printf "1.8.r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd smw

  # Fix line breaks and permission
  dos2unix configure
  chmod +x configure

  # Change data directory
  sed -i "s|usr/share/games/smw|usr/share/smw|" configure

  # Fixes for gcc warnings and libpng >1.5
  patch -Np0 < ../gcc.patch
  patch -Np0 < ../libpng.patch
}

build() {
  cd smw

  ./configure
  make
}

package() {
  cd smw
  # Not using "make install" because of different data directory

  # Install binaries
  msg2 "Installing binaries"
  install -Dm755 smw "$pkgdir"/usr/bin/smw
  install -Dm755 leveledit "$pkgdir"/usr/bin/smw-leveledit
  install -Dm755 worldedit "$pkgdir"/usr/bin/smw-worldedit

  # Install data folders
  msg2 "Installing data folders"
  install -d "$pkgdir"/usr/share/smw/
  for _datadir in gfx maps music sfx tours worlds; do
    cp -r $_datadir "$pkgdir"/usr/share/smw/
  done

  # Set sane permissions
  find "$pkgdir"/usr/share/smw -type d -exec chmod 755 {} \;
  find "$pkgdir"/usr/share/smw -type f -exec chmod 644 {} \;

  # Install application shortcut and icon
  install -Dm644 ../smw.desktop "$pkgdir"/usr/share/applications/smw.desktop
  install -Dm644 ../smw.png "$pkgdir"/usr/share/pixmaps/smw.png
}
