# Maintainer: carstene1ns <arch carsten-teibes de>

pkgname=easyrpg-player-git
_gitname=Player
pkgver=0.1.2.0.g6449223
pkgrel=1
pkgdesc="EasyRPG Player aims to be a free, RPG Maker 2000/2003 interpreter (development version)."
arch=('i686' 'x86_64')
url="https://easy-rpg.org/"
license=('GPL3')
conflicts=('easyrpg-player')
provides=('easyrpg-player')
makedepends=('cmake>=2.8' 'boost' 'git')
depends=('sdl_mixer' 'freetype2' 'pixman' 'libpng' 'libjpeg' 'boost-libs' 'openal' 'libsndfile' 'lua' 'expat')
install="$pkgname.install"
source=("git://github.com/EasyRPG/Player.git"
        "$pkgname.install")
md5sums=('SKIP'
         'bc45142571f7077981a2da6cd2a21bd1')
sha256sums=('SKIP'
            '80ead4ae70a3192e3e16b9ad36cdd40b0a6271e1a082e574687ccbadd62404ca')

pkgver() {
  cd $_gitname

  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

prepare () {
  cd "$srcdir"

  # remove old build
  rm -rf build
}

build () {
  cd "$srcdir"

  mkdir build
  cd build

  cmake ../Player -DCMAKE_EXE_LINKER_FLAGS="-Wl,--no-as-needed" -DCMAKE_INSTALL_PREFIX="/usr"

  make
}

package () {
  cd "$srcdir/build"

  DESTDIR="$pkgdir" make install
}
