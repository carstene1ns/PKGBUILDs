# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>

_pkgbase=blinkensisters
pkgname=$_pkgbase-svn
pkgver=r1084
pkgrel=1
pkgdesc="Open source jump'n'run game in 80s retro style (aka 'Hunt for The Lost Pixels', development version)"
arch=('i686' 'x86_64')
url="http://blinkensisters.at"
license=('GPL2')
depends=('sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'libtheora')
makedepends=('cmake')
provides=("$_pkgbase")
conflicts=("$_pkgbase")
source=($_pkgbase::"svn+http://svn.code.sf.net/p/blinkensisters/code")
md5sums=('SKIP')

pkgver() {
  cd $_pkgbase
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd $_pkgbase

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr -DUSE_THEORA=YES
  make
}

package() {
  cd $_pkgbase

  make DESTDIR="$pkgdir" install
}
