# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>

pkgname=phrozn
pkgver=0.5.6
pkgrel=1
pkgdesc="Extremely flexible static site generator written in PHP."
arch=('any')
url="http://phrozn.info"
license=('APACHE')
depends=('php')
makedepends=('php-composer')
source=($pkgname-$pkgver.tar.gz::"https://github.com/Pawka/phrozn/archive/$pkgver.tar.gz")
sha256sums=('1af70a4e60dc91434ba4d942de13151709bb82c978be76e433b5177d1c788560')

prepare() {
  cd $pkgname-$pkgver
  # download libraries
  composer install
  # remove repository data (20mb)
  find vendor -name .git -type d -exec rm -rf '{}' +
}

package() {
  cd $pkgname-$pkgver

  install -d "$pkgdir"/usr/{bin,share/{webapps,doc}/phrozn}
  cp -r Phrozn app bin configs extras skeleton vendor phrozn.png \
    "$pkgdir"/usr/share/webapps/phrozn
  install -m644 CONTRIBUTORS NOTICE README.textile "$pkgdir"/usr/share/doc/phrozn
  ln -s /usr/share/webapps/phrozn/bin/phrozn.php "$pkgdir"/usr/bin/phrozn
}
