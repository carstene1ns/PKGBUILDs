# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Marc poiroud <marci1@archlinux.fr>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>
# Contributor: Laurie Clark-Michalek <bluepeppers@archlinux.us>

pkgname=megamario
pkgver=1.7
pkgrel=2
pkgdesc="A Super Mario Bros clone with better graphics and new levels"
arch=('i686' 'x86_64')
url="http://mmario.sourceforge.net/"
license=('LGPL2.1')
depends=('libgl' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
source=("http://downloads.sourceforge.net/mmario/MegaMario_v${pkgver}_full.zip")
md5sums=('9550b20f7c2754cd8f94ebf309becf62')
sha256sums=('243ebf03cb1e5c5e7cead48d9a988ffb9a1c94f9427f71ea559ad047486c9e72')

prepare() {
  # set reasonable max path length
  sed -i "s/PATH_MAX/1024/" src/Global.h

  # this fixes the mp3 dir, but playing mp3 causes segfaults, so ignoring it for now
  #sed -i "s|mp3music/|/usr/share/megamario/sfx/&|" src/{functions,main}.cpp

  # fix segfaults with missing images (replaces them with error image)
  sed -i "s|^\t\treturn 0;|\t\t// do not return yet|" src/functions.cpp
}

build() {
  make PREFIX=/usr
}

package() {
  # install game files
  make PREFIX="$pkgdir/usr" install
  cp -r mp3music "$pkgdir"/usr/share/$pkgname/sfx

  # crappy makefile fix
  find "$pkgdir"/usr/share/megamario -type d -exec chmod 755 {} \;
  find "$pkgdir"/usr/share/megamario -type f -exec chmod 644 {} \;

  # this should fix some crashes (thx slackware guys!)
  ln -s Iceland.jpg "$pkgdir"/usr/share/megamario/gfx/background/iceland.jpg
  ln -s canyon.jpg "$pkgdir"/usr/share/megamario/gfx/background/Canyon.jpg

  # install desktop entry
  install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

  # install documentation
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -m644 {CONTROLS,fixes_v1.7,readme}.txt "$pkgdir"/usr/share/doc/$pkgname
}
