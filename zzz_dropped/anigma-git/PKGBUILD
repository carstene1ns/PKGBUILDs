# Maintainer: 404
# Contributor: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Anomander <anomander[at]freenet[dot]de>

pkgname=anigma-git
pkgver=2.2.1.r39.b3d7244
pkgrel=1
pkgdesc="A colourful arcade logic game - also known as puzz-le (development version)"
url="http://github.com/icefox/anigma"
arch=('i686' 'x86_64')
license=('BSD')
depends=('qt4')
makedepends=('git')
source=(anigma::"git+https://github.com/icefox/anigma.git")
md5sums=('SKIP')

pkgver() {
  cd anigma

  printf "2.2.1.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd anigma

  # fix for compilation with qt4
  sed 's|qmake |qmake-qt4 |g' -i configure
}

build() {
  cd anigma

  ./configure
  make
}

package() {
  cd anigma

  make INSTALL_ROOT="$pkgdir" install

  # fix icon dir
  install -d "$pkgdir"/usr/share/pixmaps
  mv "$pkgdir"/usr/share/applications/anigma.png "$pkgdir"/usr/share/pixmaps

  # license
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
