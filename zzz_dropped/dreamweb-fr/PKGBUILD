# Maintainer: 404
# Contributor: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Dennis Hamester <dennis.hamester [AT] gmail [DOT] com>

pkgname=dreamweb-fr
pkgver=1.0
pkgrel=1
pkgdesc="A top-down adventure game set in a gritty futuristic dystopian city - french language"
arch=('any')
url="http://www.scummvm.org/"
license=('custom: Freeware')
depends=('scummvm')
source=("http://downloads.sourceforge.net/scummvm/dreamweb-cd-fr-$pkgver.zip"
        "$pkgname.sh")
sha256sums=('bf2b6b758f8c212dec6ec0d387ba19dd9d4379d69668709aedcc93d679094a41'
            '4908b490d53dc90a33afe7ecd3d1a192b0ef14db24df7c0ca2d7748aaa957b1b')
# avoids unnecessary stripping and recompression
options=('!strip')
PKGEXT='.pkg.tar'

package() {
  # launcher
  install -Dm755 $pkgname.sh "$pkgdir"/usr/bin/$pkgname

  # data
  install -d "$pkgdir"/usr/share/$pkgname/french
  install -m644 DREAMW*.* "$pkgdir"/usr/share/$pkgname
  install -m644 track1.flac "$pkgdir"/usr/share/$pkgname
  install -m644 FRENCH/*.RAW "$pkgdir"/usr/share/$pkgname/french
  rm "$pkgdir"/usr/share/$pkgname/DREAMWFR.EXE

  # license
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
