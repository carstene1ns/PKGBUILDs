# Maintainer: cars10t <carsten.1 gmx.de>

pkgname=eclipse-eclemma
pkgver=2.2.0
pkgrel=1
pkgdesc="A free Java code coverage tool for Eclipse"
url="http://www.eclemma.org/"
arch=('any')
license=('EPL')
depends=('eclipse')
source=("https://github.com/downloads/jacoco/${pkgname/eclipse-}/${pkgname/eclipse-}-${pkgver}.zip")
md5sums=('d9eb68c3f6ccfbd7ea4b1ed888e342da')

# no build() needed
package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}-${pkgver}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
