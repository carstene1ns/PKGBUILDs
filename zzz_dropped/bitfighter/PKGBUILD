# Maintainer: carstene1ns <arch carsten-teibes de>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=bitfighter
pkgver=0.19
pkgrel=1
pkgdesc="A fast-paced team-based outer-space multi-player combat game"
arch=('i686' 'x86_64')
url="http://bitfighter.org/"
license=('GPL')
depends=('sdl2' 'libpng' 'libvorbis' 'libmodplug' 'openal' 'speex')
makedepends=('cmake' 'libmysqlclient' 'glu')
source=(http://$pkgname.googlecode.com/files/$pkgname-${pkgver//./}.tar.gz
        $pkgname.png
        $pkgname.desktop)
md5sums=('55799c5bc725e2969cfd41c293270de2'
         '8b28490af9deadd0a1a104ae372f5b08'
         'SKIP')
sha256sums=('9a1d72ff811387e4cedd1991608395bfec8d62a6a7e441abd1c866e079284441'
            '67fa2789e9ba0f51a6b6e3a84bcafab2e2104f131d964039707932b8c228db65'
            'SKIP')

prepare() {
  rm -rf build
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname-${pkgver//./}

  make
}

package() {
  cd $pkgname-${pkgver//./}

  # install game ressources and executable
  install -d "$pkgdir"/usr/share/$pkgname "$pkgdir"/usr/bin
  cp -r resource/* exe/*.{cfg,ini} "$pkgdir"/usr/share/$pkgname
  install -m755 exe/$pkgname "$pkgdir"/usr/bin/$pkgname

  # install a desktop entry
  install -Dm644 ../$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

  # install documentation and license
  install -Dm644 doc/README.txt "$pkgdir"/usr/share/doc/$pkgname/README
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
