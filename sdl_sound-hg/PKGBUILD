# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

_pkgbase=sdl_sound
pkgname=$_pkgbase-hg
pkgver=r596.719dade41745
pkgrel=1
pkgdesc="A library to decode several popular sound file formats (development version)"
arch=('i686' 'x86_64')
url="http://icculus.org/SDL_sound/"
license=('LGPL')
depends=('sdl' 'libmodplug' 'libvorbis' 'flac' 'speex')
conflicts=($_pkgbase)
provides=($_pkgbase)
source=($_pkgbase::"hg+http://hg.icculus.org/icculus/SDL_sound"
        'physfs-renamed-export.patch')
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd $_pkgbase

  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  cd $_pkgbase

  # fix deprecated physfs declaration
  patch -Np1 < ../physfs-renamed-export.patch
}

build() {
  cd $_pkgbase

  ./bootstrap
  ./configure --prefix=/usr --disable-static --disable-mikmod
  make
}

package() {
  cd $_pkgbase

  make DESTDIR="$pkgdir/" install
}
