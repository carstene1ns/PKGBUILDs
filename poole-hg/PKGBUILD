# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Marco S <marcosiviero.mobile@gmail.com>
# Contributer: Paul Barker <paul@paulbarker.me.uk>

_pkgbase=poole
pkgname=$_pkgbase-hg
pkgver=192.5ce9ce17942c
pkgrel=1
pkgdesc="A damn simple static website generator"
arch=('i686' 'x86_64')
url="https://bitbucket.org/obensonne/poole"
license=('GPL3')
conflicts=('poole-py3-hg')
depends=('python2-markdown')
makedepends=('mercurial')
source=("hg+$url")
md5sums=('SKIP')

pkgver() {
  cd $_pkgbase

  printf "%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  # patch theme directory
  sed -i "s|opj(HERE, 'themes')|'/usr/share/$_pkgbase/themes'|" \
    $_pkgbase/$_pkgbase.py
  # use python2
  sed 's|env python|env python2|' -i $_pkgbase/$_pkgbase.py
}

package() {
  install -Dm755 $_pkgbase/$_pkgbase.py "$pkgdir"/usr/bin/$_pkgbase
  install -d "$pkgdir"/usr/share/$_pkgbase
  cp -r $_pkgbase/themes "$pkgdir"/usr/share/$_pkgbase
}
