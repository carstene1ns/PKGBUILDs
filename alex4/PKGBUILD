# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: megadriver <megadriver at gmx dot com>

pkgname=alex4
pkgver=1.1
pkgrel=5
pkgdesc="Help Alex the Allegator rescue Lola before the humans make a pair of shoes out of her!"
url="http://allegator.sourceforge.net"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dumb')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/allegator/Alex4/source%20and%20data/alex4src_data.zip"
        "$pkgname.png"
        "unix-port.patch"
        "allegro-4.2.patch"
        "$pkgname.desktop")
md5sums=('c81b8e3813300a0cfbe828bcdff60fda'
         '1c21affded614148ad06deb1bb59086c'
         'SKIP' 'SKIP' 'SKIP')
sha256sums=('d266d7fba64fbfedf13240d3d0eb21b8bacbedeaa5f22b26a27d472c8d23f103'
            '236a8d163ee8aa94073aa1ec969a85e887b3c70fefbcd03b1f41b93b09fa32c6'
            'SKIP' 'SKIP' 'SKIP')

prepare() {
  cd alex4src

  # make compatible with modern environment
  patch -Np1 < ../unix-port.patch
  patch -Np1 < ../allegro-4.2.patch
}

build() {
  cd alex4src/src

  LDFLAGS=-lm make PREFIX=/usr
}

package() {
  cd alex4src

  make -C src PREFIX="$pkgdir/usr" install

  install -Dm644 alex4.ini "$pkgdir"/usr/share/alex4/alex4.ini

  # install desktop entry
  install -Dm644 ../$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
