# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

_pkgbase=sdl_sound
pkgname=$_pkgbase-patched-hg
pkgver=r596.719dade41745
pkgrel=1
pkgdesc="A library to decode several popular sound file formats, patched for SDL2 (development version)"
arch=('i686' 'x86_64')
url="http://icculus.org/SDL_sound/"
license=('LGPL')
depends=('sdl2' 'libmodplug' 'libvorbis' 'flac' 'speex')
conflicts=($_pkgbase)
provides=($_pkgbase)
source=($_pkgbase::"hg+http://hg.icculus.org/icculus/SDL_sound"
        'pkgconfig.patch'
        'physfs-renamed-export.patch')
sha256sums=('SKIP'
            'c2ebc5487c678ca4d77f5530a01c14602b9794861f4f75a1097fe47f9a8bd7ff'
            'd7bd96390d9bc877c0204922c7c4666cadfdccc5e6c0cfcf9477d113377f5d10')

pkgver() {
  cd $_pkgbase

  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  cd $_pkgbase

  # fix deprecated physfs declaration
  patch -Np1 < ../physfs-renamed-export.patch
  # patch for SDL2 and pkgconfig support
  patch -Np1 < ../pkgconfig.patch
}

build() {
  cd $_pkgbase

  ./bootstrap
  ./configure --prefix=/usr --disable-static --disable-mikmod
  make
}

package() {
  cd $_pkgbase

  make DESTDIR="$pkgdir/" install
}
