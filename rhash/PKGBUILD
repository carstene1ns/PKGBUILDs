# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: 謝致邦 <Yeking@Red54.com>
# Contributor: cantabile <cantabile dot desu at gmail dot com>
# Contributor: reMiND <detanator@gmail.com>

pkgname=rhash
pkgver=1.3.1
pkgrel=3
pkgdesc='Utility for verifying hash sums (SFV, CRC, etc). Supports lots of algorithms.'
arch=('i686' 'x86_64')
url='http://rhash.anz.ru/?l=en'
license=('custom' 'MIT')
depends=('glibc')
backup=('etc/rhashrc')
#options=('staticlibs')
source=("http://downloads.sourceforge.net/rhash/rhash-$pkgver-src.tar.gz")
sha256sums=('cfa6f9b926910fd0e191b7f126b1f3f2ffcc2684977d448faa9b8c4532a65804')

build() {
  cd rhash-$pkgver

  make ADDCFLAGS="$CFLAGS" ADDLDFLAGS="$LDFLAGS" build-shared lib-static
}

package() {
  cd rhash-$pkgver

  # program
  make PREFIX=/usr DESTDIR="$pkgdir" install-shared
  # library
  make -C librhash PREFIX=/usr DESTDIR="$pkgdir" install-headers install-lib-shared install-lib-static
  # license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/rhash/LICENSE
}
